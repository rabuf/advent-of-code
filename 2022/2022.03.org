#+STARTUP: indent contents
#+OPTIONS: num:nil toc:nil
* Day 03
** Executing this code
If you have a lisp installation, emacs, org-mode, and org-babel
support for lisp installed you can run this by:
1. Starting slime (=M-x slime=)
2. Typing =C-c C-c= in the block [[initialize][initialize]].
3. In the repl type =(in-package :aoc-2022-03)=
4. Typing =C-c C-c= in the block [[answers][answers]]
** Initial stuffs
*** Packages to load
#+NAME: packages
#+BEGIN_SRC lisp :results silent
  (unless (find-package :cl-ppcre)
    (ql:quickload "cl-ppcre"))
  (unless (find-package :parseq)
    (ql:quickload "parseq"))
  (unless (find-package :lparallel)
    (ql:quickload "lparallel"))
  (unless (find-package :fiveam)
    (ql:quickload "fiveam"))
  (unless (find-package :series)
    (ql:quickload "series"))
  (unless (find-package :cl-permutation)
    (ql:quickload "cl-permutation"))
  (unless (find-package :bordeaux-threads)
    (ql:quickload "bordeaux-threads"))
#+END_SRC
*** Create package for this day
#+NAME: initialize
#+BEGIN_SRC lisp :noweb yes :results silent
  <<packages>>
  (defpackage :aoc-2022-03
    (:use :common-lisp
          :parseq
          :fiveam)
    (:export :problem-a
             :problem-b))
  (in-package :aoc-2022-03)
#+END_SRC
** Input
#+NAME: read-input
#+BEGIN_SRC lisp :results silent
  (defun process-stream (in)
    (loop
      for line = (read-line in nil)
      while line
      collect line))

  (defun read-input (file)
    (with-open-file (in file)
      (process-stream in)))
#+END_SRC
#+NAME: input
#+BEGIN_SRC lisp :noweb yes :results silent
  (defparameter *input*
    (read-input "input/03.txt"))
#+END_SRC
** Part 1
#+NAME: problem-a
#+BEGIN_SRC lisp :noweb yes :results silent
  (defun priority-value (c)
    (cond ((upper-case-p c)
           (+ (- (char-code c) (char-code #\A)) 27))
          (t
           (+ (- (char-code c) (char-code #\a)) 1))))
  (defun priority (rucksack)
    (let* ((mid (floor (length rucksack) 2))
           (first (subseq rucksack 0 mid))
           (second (subseq rucksack mid)))
      (loop for c across first
            while (not (position c second))
            finally (return (priority-value c)))))


  (defun priority-sum (rucksacks)
    (reduce #'+ (mapcar #'priority rucksacks)))

  (defun problem-a () (format t "Problem 03 A: ~a~%" (priority-sum *input*)))
#+END_SRC
** Part 2
#+NAME: problem-b
#+BEGIN_SRC lisp :noweb yes :results silent
  (defun badge-priority (r1 r2 r3)
    (loop for c across r1
          while (not (and (position c r2)
                          (position c r3)))
          finally (return (priority-value c))))

  (defun badge-sum (rucksacks)
    (loop for (r1 r2 r3) on rucksacks by #'cdddr
          sum (badge-priority r1 r2 r3)))

  (defun problem-b () (format t "Problem 03 B: ~a~%" (badge-sum *input*)))
#+END_SRC
** Putting it all together
#+NAME: structs
#+BEGIN_SRC lisp :noweb yes :results silent

#+END_SRC
#+NAME: functions
#+BEGIN_SRC lisp :noweb yes :results silent
  <<read-input>>
  <<input>>
#+END_SRC
#+NAME: answers
#+BEGIN_SRC lisp :results output :exports both :noweb yes :tangle no
  <<initialize>>
  <<structs>>
  <<functions>>
  <<input>>
  <<problem-a>>
  <<problem-b>>
  (problem-a)
  (problem-b)
#+END_SRC
** Answer
#+RESULTS: answers
: Problem 03 A: 7821
: Problem 03 B: 2752
** Test Cases
#+NAME: test-cases
#+BEGIN_SRC lisp :results output :exports both
  (def-suite aoc.2022.03)
  (in-suite aoc.2022.03)

  (defparameter *sample-input*
    "vJrwpWtwJgWrhcsFMMfFFhFp
  jqHRNqRjqzjGDLGLrsFMfFZSrLrFZsSL
  PmmdzqPrVvPwwTWBwg
  wMqvLMZHhHMvwLHjbvcjnnSBnvTQFn
  ttgJtRGJQctTZtZT
  CrZsJsPPZsGzwwsLwLmpwMDw")

  (test priority
    (with-input-from-string (in *sample-input*)
      (let ((rucksacks (process-stream in))
            (expected '(16 38 42 22 20 19)))
        (loop for r in rucksacks
              for e in expected
              do (is (= (priority r) e))))))

  (test priority-sum
    (is (= 7821 (priority-sum *input*)))
    (with-input-from-string (in *sample-input*)
      (let ((rucksacks (process-stream in)))
        (is (= 157 (priority-sum rucksacks))))))

  (test badge-priority
    (with-input-from-string (in *sample-input*)
      (let ((rucksacks (process-stream in))
            (expected '(18 52)))
        (loop for (r1 r2 r3) on rucksacks by #'cdddr
              for p = (badge-priority r1 r2 r3)
              for e in expected
              do (is (= p e))))))

  (test badge-sum
    (is (= 2752 (badge-sum *input*)))
    (with-input-from-string (in *sample-input*)
      (let ((rucksacks (process-stream in)))
        (is (= 70 (badge-sum rucksacks))))))
  (run! 'aoc.2022.03)
#+END_SRC
** Test Results
#+RESULTS: test-cases
#+begin_example

Running test suite AOC.2022.03
 Running test PRIORITY ......
 Running test PRIORITY-SUM ..
 Running test BADGE-PRIORITY ..
 Running test BADGE-SUM ..
 Did 12 checks.
    Pass: 12 (100%)
    Skip: 0 ( 0%)
    Fail: 0 ( 0%)
#+end_example
** Thoughts
