#+STARTUP: indent contents
#+OPTIONS: num:nil toc:nil
* Day 05
** Executing this code
If you have a lisp installation, emacs, org-mode, and org-babel
support for lisp installed you can run this by:
1. Starting slime (=M-x slime=)
2. Typing =C-c C-c= in the block [[initialize][initialize]].
3. In the repl type =(in-package :aoc-2021-05)=
4. Typing =C-c C-c= in the block [[answers][answers]]
** Initial stuffs
*** Packages to load
#+NAME: packages
#+BEGIN_SRC lisp :results silent
  (unless (find-package :cl-ppcre)
    (ql:quickload "cl-ppcre"))
  (unless (find-package :iterate)
    (ql:quickload "iterate"))
  (unless (find-package :parseq)
    (ql:quickload "parseq"))
  (unless (find-package :lparallel)
    (ql:quickload "lparallel"))
  (unless (find-package :fiveam)
    (ql:quickload "fiveam"))
  (unless (find-package :series)
    (ql:quickload "series"))
  (unless (find-package :cl-permutation)
    (ql:quickload "cl-permutation"))
  (unless (find-package :bordeaux-threads)
    (ql:quickload "bordeaux-threads"))
  (unless (find-package :zpng)
    (ql:quickload "zpng"))
#+END_SRC
*** Create package for this day
#+NAME: initialize
#+BEGIN_SRC lisp :noweb yes :results silent
  <<packages>>
  (defpackage :aoc-2021-05
    (:use :common-lisp
          :parseq
          :zpng
          :fiveam)
    (:export :problem-a
             :problem-b))
  (in-package :aoc-2021-05)
#+END_SRC
** Input
#+NAME: read-input
#+BEGIN_SRC lisp :results silent
  (defun read-input (file)
    (with-open-file (in file)
      (loop
         for line = (read-line in nil)
         while line
         collect (mapcar #'parse-integer (cl-ppcre:all-matches-as-strings "(\\d+)" line)))))
#+END_SRC
#+NAME: input
#+BEGIN_SRC lisp :noweb yes :results silent
  (defparameter *input*
    (read-input "input/05.txt"))
#+END_SRC
** Part 1
Each row represents a line segment with two pairs of coordinates
describing an endpoint. The question is how many points are shared by
at least two lines, only considering the horizontal and vertical lines
(either x's are the same or y's).

As is convention, I'll use complex numbers for the points.
#+NAME: to-grid
#+BEGIN_SRC lisp :noweb yes :results silent
  (defun pairs-to-grid (pairs)
    (loop for (x0 y0 x1 y1) in pairs
       with grid = (make-hash-table)
       when (= x0 x1)
       do (loop for i from (min y0 y1) to (max y0 y1)
             do (incf (gethash (complex x0 i) grid 0)))
       when (= y0 y1)
       do (loop for i from (min x0 x1) to (max x0 x1)
             do (incf (gethash (complex i y0) grid 0)))
       finally (return grid)))
  (defun count-overlapping (grid)
    (loop for v being the hash-values of grid
         count (> v 1)))
#+END_SRC
#+NAME: problem-a
#+BEGIN_SRC lisp :noweb yes :results silent
  (defun problem-a () (format t "Problem 05 A: ~a~%" (count-overlapping (pairs-to-grid *input*))))
#+END_SRC
** Part 2
Now we have to include the diagonal lines, same problem in the end.
#+NAME: to-grid-with-diagonals
#+BEGIN_SRC lisp :noweb yes :results silent
  (defun pairs-to-grid-all (pairs)
    (loop for (x0 y0 x1 y1) in pairs
       with grid = (make-hash-table)
       when (= x0 x1)
       do (loop for i from (min y0 y1) to (max y0 y1)
             do (incf (gethash (complex x0 i) grid 0)))
       when (= y0 y1)
       do (loop for i from (min x0 x1) to (max x0 x1)
             do (incf (gethash (complex i y0) grid 0)))
       when (and (/= x0 x1) (/= y0 y1))
       do (loop
             with dir = (if (= x0 (min x0 x1)) (signum (- y1 y0)) (signum (- y0 y1)))
             with start-y = (if (= x0 (min x0 x1)) y0 y1)
             for i from (min x0 x1) to (max x0 x1)
             for offset = (* dir (- i (min x0 x1)))
             do (incf (gethash (complex i (+ start-y offset)) grid 0)))
       finally (return grid)))
  (defun print-grid (grid)
    (let (min-x min-y max-x max-y)
      (maphash #'(lambda (k v) (declare (ignore v))
                   (unless min-x (setf min-x (realpart k)))
                   (unless min-y (setf min-y (imagpart k)))
                   (unless max-x (setf max-x (realpart k)))
                   (unless max-y (setf max-y (imagpart k)))
                   (setf min-x (min min-x (realpart k)))
                   (setf max-x (max max-x (realpart k)))
                   (setf min-y (min min-y (imagpart k)))
                   (setf max-y (max max-y (imagpart k))))
               grid)
      (loop for i from min-y to max-y
       do (loop for j from min-x to max-x
             do (format t "~A" (gethash (complex j i) grid #\.)))
         (terpri))))
#+END_SRC
#+NAME: problem-b
#+BEGIN_SRC lisp :noweb yes :results silent
  (defun problem-b () (format t "Problem 05 B: ~a~%" (count-overlapping (pairs-to-grid-all *input*))))
#+END_SRC
** Putting it all together
#+NAME: structs
#+BEGIN_SRC lisp :noweb yes :results silent

#+END_SRC
#+NAME: functions
#+BEGIN_SRC lisp :noweb yes :results silent
  <<read-input>>
  <<input>>
#+END_SRC
#+NAME: answers
#+BEGIN_SRC lisp :results output :exports both :noweb yes :tangle no
  <<initialize>>
  <<structs>>
  <<functions>>
  <<input>>
  <<problem-a>>
  <<problem-b>>
  (problem-a)
  (problem-b)
#+END_SRC
** Answer
#+RESULTS: answers
: Problem 05 A: 4728
: Problem 05 B: 17717
** Test Cases
#+NAME: test-cases
#+BEGIN_SRC lisp :results output :exports both
  (def-suite aoc.2021.05)
  (in-suite aoc.2021.05)
  (defparameter *test-input*
    (read-input "test/05.txt"))
  (test both-parts
    (is (= 5 (count-overlapping (pairs-to-grid *test-input*))))
    (is (= 12 (count-overlapping (pairs-to-grid-all *test-input*)))))
  (run! 'aoc.2021.05)
#+END_SRC
** Test Results
#+RESULTS: test-cases
: 
: Running test suite AOC.2021.05
:  Running test BOTH-PARTS ..
:  Did 2 checks.
:     Pass: 2 (100%)
:     Skip: 0 ( 0%)
:     Fail: 0 ( 0%)
** Visualization
I haven't done one of these before so this is a for-fun thing.
#+BEGIN_SRC lisp :results silent :noweb yes
  (defun overlap-to-color (overlap max-overlap)
    (floor (* 255 (/ overlap max-overlap))))
  (defun grid-to-png (grid filename)
    (let (min-x min-y max-x max-y max-overlap)
      ;; get bounds for image
      (maphash #'(lambda (k v)
                   (unless min-x (setf min-x (realpart k)))
                   (unless min-y (setf min-y (imagpart k)))
                   (unless max-x (setf max-x (realpart k)))
                   (unless max-y (setf max-y (imagpart k)))
                   (unless max-overlap (setf max-overlap v))
                   (setf max-overlap (max max-overlap v))
                   (setf min-x (min min-x (realpart k)))
                   (setf max-x (max max-x (realpart k)))
                   (setf min-y (min min-y (imagpart k)))
                   (setf max-y (max max-y (imagpart k))))
               grid)
      (let* ((png (make-instance 'png
                                 :color-type :grayscale
                                 :width (+ 20 (- max-x min-x))
                                 :height (+ 20 (- max-y min-y))))
             (image (data-array png)))
        (loop
           for y from min-y to max-y
           finally (write-png png filename)
           do (loop
                 for x from min-x to max-x
                 for color = (gethash (complex x y) grid 0)
                 do (setf (aref image (+ (- y min-y) 10) (+ (- x min-x) 10) 0) (overlap-to-color color max-overlap)))))))
#+END_SRC
[[./diagonal.png]]

I could play around more with the coloring but I think that's enough for one night.
** Thoughts

** Ada
Today I'll be using the =GNAT.Regpat= package that, conveniently, was
already loaded in my template (for precisely this reason). I spent
some time after completing the CL version reading through its
documentation and learned a couple new things, which will make using
it easier.
*** Runner
Simple runner.
#+BEGIN_SRC ada :tangle ada/day05.adb
  with AOC2021.Day05;
  procedure Day05 is
  begin
    AOC2021.Day05.Run;
  end Day05;
#+END_SRC
*** Specification
Specification for solution.
#+BEGIN_SRC ada :tangle ada/aoc2021-day05.ads
  package AOC2021.Day05 is
     procedure Run;
  end AOC2021.Day05;
#+END_SRC
*** Packages
#+NAME: ada-packages
#+BEGIN_SRC ada
  with GNAT.Regpat; use GNAT.Regpat;
  with Text_IO; use Text_IO;
  with Ada.Containers.Vectors;
#+END_SRC
*** Types and generics
Because it will simplify some elements, I'll go ahead and create a
couple record types for the points and the segments. Unlike the CL
version, I'll use an array to store the values. The other option is to
use one of the map containers but that's overkill for this situation.
#+NAME: types-and-generics
#+BEGIN_SRC ada
  type Point is record
     X, Y : Integer := 0;
  end record;

  type Segment is record
     A, B : Point;
  end record;

  package Segment_Vectors is new Ada.Containers.Vectors
    (Element_Type => Segment, Index_Type => Natural);
  use Segment_Vectors;

  type Map is array (Integer range <>, Integer range <>) of Natural
    with Default_Component_Value => 0;

  function Make_Map (Lower : Point; Upper : Point) return Map is
     M : Map (Lower.X..Upper.X, Lower.Y..Upper.Y);
  begin
     return M;
  end Make_Map;
#+END_SRC
Using the =Default_Component_Value= bit above saves me from having to
initialize the array values later or trust that they'll have a correct
initial value, I know exactly what it will be.
*** Implementation
**** Parsing
Using the =GNAT.Regpat= library simplifies parsing each line quite a bit.
#+NAME: ada-parse
#+BEGIN_SRC ada
  procedure Parse_Line (Line : String; S : out Segment) is
     Pattern : constant String := "(\d+),(\d+) -> (\d+),(\d+)";
     Re : constant Pattern_Matcher := Compile(Pattern);
     Matches : Match_Array (0.. Paren_Count (Re));
  begin
     Match(Re, Line, Matches);
     S.A.X := Integer'Value(Line(Matches(1).First..Matches(1).Last));
     S.A.Y := Integer'Value(Line(Matches(2).First..Matches(2).Last));
     S.B.X := Integer'Value(Line(Matches(3).First..Matches(3).Last));
     S.B.Y := Integer'Value(Line(Matches(4).First..Matches(4).Last));
  end Parse_Line;

  procedure Read_File (Filename : String; Segments : out Vector; Lower : in out Point; Upper : in out Point) is
     Input_File : File_Type;
     S : Segment;
  begin
     Upper.X := Integer'First;
     Upper.Y := Integer'First;
     Lower.X := Integer'Last;
     Lower.Y := Integer'Last;
     Open (Input_File, In_File, Filename);
     while not End_Of_File (Input_File) loop
        declare
           Line : String := Get_Line (Input_File);
        begin
           Parse_Line (Line, S);
        end;
        Segments.Append (S);
        Upper.Y := Integer'Max (Upper.Y, Integer'Max(S.A.Y, S.B.Y));
        Upper.X := Integer'Max (Upper.X, Integer'Max(S.A.X, S.B.X));
        Lower.Y := Integer'Min (Lower.Y, Integer'Min(S.A.Y, S.B.Y));
        Lower.X := Integer'Min (Lower.X, Integer'Min(S.A.X, S.B.X));
     end loop;
     Close (Input_File);
  end Read_File;
#+END_SRC
**** Common
#+NAME: ada-common
#+BEGIN_SRC ada
  function Signum(N : Integer) return Integer is (if N < 0 then -1 else (if N = 0 then 0 else 1));
  procedure Fill_Map(M : in out Map; S : Segment) is
     DX : Integer := Signum (S.B.X - S.A.X);
     DY : Integer := Signum (S.B.Y - S.A.Y);
     Distance : Integer := Integer'Max (abs (S.B.X - S.A.X), abs (S.B.Y - S.A.Y));
  begin
     for I in 0..Distance loop
        M (S.A.X + DX * I, S.A.Y + DY * I) := M (S.A.X + DX * I, S.A.Y + DY * I) + 1;
     end loop;
  end Fill_Map;

  function Count_Overlapping (M : Map) return Integer is
     Count : Integer := 0;
  begin
     for N of M loop
        if N > 1 then
           Count := Count + 1;
        end if;
     end loop;
     return Count;
  end Count_Overlapping;
#+END_SRC
**** Part 1
#+NAME: ada-part-1
#+BEGIN_SRC ada
  procedure Part_1 (Segments : Vector; M : in out Map) is
  begin
     for S of Segments loop
        if S.A.X = S.B.X or S.A.Y = S.B.Y then
           Fill_Map (M, S);
        end if;
     end loop;
  end Part_1;
#+END_SRC
**** Part 2
#+NAME: ada-part-2
#+BEGIN_SRC ada
  procedure Part_2 (Segments : Vector; M : in out Map) is
  begin
     for S of Segments loop
        if S.A.X /= S.B.X and S.A.Y /= S.B.Y then
           Fill_Map (M, S);
        end if;
     end loop;
  end Part_2;
#+END_SRC
**** Putting it all together
#+BEGIN_SRC ada :noweb yes :tangle ada/aoc2021-day05.adb
  <<ada-packages>>
  package body AOC2021.Day05 is
     <<types-and-generics>>
     <<ada-parse>>
     <<ada-common>>
     <<ada-part-1>>
     <<ada-part-2>>
     procedure Run is
        Segments : Vector;
        Lower : Point;
        Upper : Point;
        P1 : Integer;
        P2 : Integer;
     begin
        Read_File ("../input/05.txt", Segments, Lower, Upper);
        declare
           M : Map := Make_Map (Lower, Upper);
        begin
           Part_1 (Segments, M);
           P1 := Count_Overlapping (M);
           Part_2 (Segments, M);
           P2 := Count_Overlapping (M);
        end;
        Put_Line("Advent of Code 2021 - Day 05");
        Put_Line("The result for Part 1 is " & Integer'Image(P1));
        Put_Line("The result for Part 2 is " & Integer'Image(P2));
     end Run;
  end AOC2021.Day05;
#+END_SRC
*** Run the program
In order to run this you have to "tangle" the code first using =C-c
C-v C-t=.

#+BEGIN_SRC shell :tangle no :results output :exports both
  cd ada
  gnatmake day05
  ./day05
#+END_SRC

#+RESULTS:
: Advent of Code 2021 - Day 05
: The result for Part 1 is  4728
: The result for Part 2 is  17717
